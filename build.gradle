plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'

    id 'com.github.gmazzo.buildconfig' version "3.1.0"
    id 'org.jreleaser' version '1.17.0'
}

group 'bayern.steinbrecher'
version '0.1.28-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'io.soabase.record-builder:record-builder-processor:44'
    implementation 'io.soabase.record-builder:record-builder-core:44'
    implementation('com.sun.xml.bind:jaxb-ri:4.0.2') {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-release-documentation'
        exclude group: 'com.sun.xml.bind', module: 'jaxb-samples'
        exclude group: 'com.sun.xml.bind', module: 'jaxb-jxc'
    }

    testImplementation 'org.testng:testng:7.7.0'
}

java {
    modularity.inferModulePath = true
    withSourcesJar()
    withJavadocJar()
}

test {
    useTestNG()
}

buildConfig {
    buildConfigField("String", "LIB_NAME", "\"${project.name}\"")
    buildConfigField("String", "LIB_VERSION", "\"${project.version}\"")
    buildConfigField("long", "BUILD_TIME", "${System.currentTimeMillis()}L")
    packageName("bayern.steinbrecher.sepaxmlgenerator")
}

compileJava {
    options.encoding = 'UTF-8'
    options.release = 19
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }

    exclude 'bayern/steinbrecher/sepaxmlgenerator/**'
    include 'bayern/steinbrecher/sepaxmlgenerator/GenerationFailedException.java'
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'command'
    }
    checksum{
        algorithm('SHA-256')
    }
    release {
        github {
            enabled = true
        }
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    snapshotSupported = true

                    applyMavenCentralRules = false
                    sign = false
                    checksums = true
                    sourceJar = true
                    javadocJar = true
                    verifyPom = true
                }
            }
        }
    }
    announce {
        active = 'NEVER'
    }
}

publishing {
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
    publications {
        mavenLibrary(MavenPublication) {
            pom {
                name = "${project.name}"
                description = 'A library for generating multiple SEPA standard XML files from an abstract description'
                url = 'https://steinbrecher.bayern'
                licenses {
                    license {
                        name = 'The Beer-Ware License (Revision 42)'
                    }
                }
                developers {
                    developer {
                        id = 'stefanHuber'
                        name = 'Stefan Huber'
                        email = 'stefan.huber.niedling@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TrackerSB/SepaXMLGenerator.git'
                    developerConnection = 'scm:git:ssh://github.com/TrackerSB/SepaXMLGenerator.git'
                    url = 'https://github.com/TrackerSB/SepaXMLGenerator'
                }
            }
            from components.java
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenLibrary
}
